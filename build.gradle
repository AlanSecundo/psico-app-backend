plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id 'org.sonarqube' version '4.4.1.3373'
}

group = 'com.psicoclinic'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

jacoco {
	toolVersion = "0.8.10"
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'

	implementation 'org.keycloak:keycloak-spring-boot-starter:24.0.4'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'com.h2database:h2'

	compileOnly 'org.projectlombok:lombok:1.18.30'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	annotationProcessor 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
}

tasks.named("test").configure {
	useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
	options.annotationProcessorPath = configurations.annotationProcessor
}

tasks.named("jacocoTestReport").configure {
	reports {
		xml.required.set(true)
		html.required.set(true)
	}
	finalizedBy(tasks.named("openJacocoHtml"))
}

tasks.register("openJacocoHtml") {
	doLast {
		def reportFile = layout.buildDirectory.file("reports/jacoco/test/html/index.html").get().asFile
		if (reportFile.exists()) {
			java.awt.Desktop.desktop.browse(reportFile.toURI())
		} else {
			println "Relatório não encontrado: ${reportFile}"
		}
	}
}
